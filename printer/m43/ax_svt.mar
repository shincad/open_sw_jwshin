	.PROGRAM	aplsvt
	.HEADING	"M4324 AX SAVE TEXT BUFFER & FS SEQUENCE MODULE"
	.LIST		NOEXP,NOCOND
;********************************************************
;							*
;	M4324 AX SAVE TEXT BUFFER & FS SEQUENCE MODULE	*
;							*
;	FILE NAME	AX_SVT.MAR			*
;	CREATED 	05/JUN/1991			*
;********************************************************
;
	.EXPORT 	STPVF, STLSM, SVANK, SVKNJ
	.EXPORT 	STCOMN,CKTXTB,SVKCM, SVCCM
	.EXPORT 	SVFCM, GTSPD, CHKPXN,SETPR1
	.EXPORT 	FSCTL, SLKNJ, CNKNJ, SLDB1
	.EXPORT 	STCND, LFCD1, SETPRN,CANCD
	.EXPORT 	DELCD, FNCCD, CHKPEO,STENDM
	.EXPORT 	STPRNT,LINFD, SETPEL
	.EXPORT 	FFCD2, CRCD,  HVCON		; 911205
;
	.IMPORT 	GETC,  CKDT01,D2STX, ESCCT
	.IMPORT 	DFGAJ, INBFP, INBFC
	.IMPORT 	GETPFP,GPTPM, CHKDP3,FJUST
	.IMPORT 	GETPC1,SVDTK2,SVDTA3,CHKMD
	.IMPORT 	UPTBFP,CKSPDB,NXCLM, BUZT
	.IMPORT 	FS58R, TGXXP			; 920406
;	.IMPORT 	KSTARD, KSTOPD				   ;\ 920311
;
	.INCLUDE	"AX_BUF.H"
	.INCLUDE	"AX_DLD.H"
	.INCLUDE	"AX_DP2.H"
	.INCLUDE	"AX_DP3.H"
	.INCLUDE	"AX_EEP.H"
	.INCLUDE	"AX_IOS.H"
	.INCLUDE	"AX_RAM.H"
	.INCLUDE	"AX_TBF.H"
	.INCLUDE	"COMMON.H"
	.INCLUDE	"GAIFP.H"
	.INCLUDE	"SYSLIB.H"
;
;
;	<< EACH CONSTANT >>
;
QKNJHS: .EQU	  7			; KANJI HIGH SPEED MODE BIT
QAKCDH: .EQU	  8			; EXPAND ANK HIGH CODE BIT
XRELUL: .EQU	H'80                    ; RELATIVE POSITION FOR UNDERLINE
	.PAGE
	.SECTION	APL2,CODE,ALIGN=16
;
;	<< EXPANSION FUNCTION SEQUENCE >>
;
FNCCD:	MOV.B	R0,R4			; SAVE OFFSET
	MULXU.B #3,R4			; OFFSET x 3
	JSR	@(FNCTB:16,R4)		; EACH FUNCTION PROCESS
	RTS
;
;	<< EXPANSION FS SEQUENCE >>
;
FSCTL:	JSR	@GETC			; GET DATA
	AND.B	#ZN-H'80,R0             ; CLEAR MSB
	CMP.B	#"~",R0                 ;
	BHI	FSCT_2			; BR. IF IGNORE CODE
	MOV.B	R0,R4			; SAVE OFFSET
	MULXU.B #3,R4			; OFFSET x 3
	JMP	@(FSTBL:16,R4)		; EASH FS SEQUENSE PROCESS
	;
FSCT_2: RTS
	.PAGE
;
;	<< FUNCTION CODE TABLE >>
;
FNCTB:	BRA	FSERR:16		; (00) NUL
	BRA	FSERR:16		; (01) SOH
	JMP	@D2STX			;*(02) STX STX Sequence (EEPROM Command)
	BRA	FSERR:16		; (03) ETX
	BRA	FSERR:16		; (04) EOT
	BRA	FSERR:16		; (05) ENQ
	BRA	FSERR:16		; (06) ACK
	BRA	BELCD:16		; (07) BEL Bell
	BRA	BSPCD:16		; (08) BS  Backspace
	BRA	HTBCD:16		; (09) HT  Horizontal Tab
	BRA	ESLFCD:16		; (0A) LF  Line Feed
	BRA	ESVTCD:16		; (0B) VT  Vertical Tab
	BRA	ESFFCD:16		; (0C) FF  Form Feed
	BRA	CRCD:16 		; (0D) CR  Carriage Return
	BRA	SLDB1:16		; (0E) SO  Select Double-width Mode <1 Line>
	BRA	STCND:16		; (0F) SI  Set Condense Mode
	BRA	FSERR:16		; (10) DLE
	BRA	FSERR:16		; (11) DC1 Select Printer
	BRA	CNCND:16		; (12) DC2 Cancel Condense Mode
	BRA	DC3CD:16		; (13) DC3 Deselect Printer
	BRA	CNDB1:16		; (14) DC4 Cancel Double-width Mode <1 Line>
	BRA	FSERR:16		; (15) NAK (GRAPHIC TABLE -> PRINT DATA < SECTION >)
	BRA	FSERR:16		; (16) SYN
	BRA	FSERR:16		; (17) ETB
	BRA	CANCD:16		; (18) CAN Cancel
	BRA	FSERR:16		; (19) EM
	BRA	FSERR:16		; (1A) SUB
	JMP	@ESCCT			; (1B) ESC ESCAPE Sequence
	JMP	@FSCTL			;*(1C) FS  FS Sequence
	BRA	FSERR:16		; (1D) GS
	BRA	FSERR:16		; (1E) RS
	BRA	FSERR:16		; (1F) US
	.PAGE
;
;	<< FS SEQUENCE TABLE >>
;
FSTBL:	BRA	FSERR:16		; (00) NUL
	BRA	FSERR:16		; (01) SOH
	BRA	FSERR:16		; (02) STX STX Sequence (EEPROM Command)
	BRA	FSERR:16		; (03) ETX
	BRA	FSERR:16		; (04) EOT
	BRA	FSERR:16		; (05) ENQ
	BRA	FSERR:16		; (06) ACK
	BRA	FSERR:16		; (07) BEL
	BRA	FSERR:16		; (08) BS
	BRA	FSERR:16		; (09) HT
	BRA	FSERR:16		; (0A) LF
	BRA	FSERR:16		; (0B) VT
	BRA	FSERR:16		; (0C) FF
	BRA	FSERR:16		; (0D) CR
	BRA	SLDB1:16		; (0E) SO  Select Double-width Mode <1 Line>
;920317 BRA	SLKHF:16		; (0F) SI  Select Kanji Half Mode
	BRA	STCND:16		; (0F) SI  Half Mode  920317
	BRA	FSERR:16		; (10) DLE
	BRA	FSERR:16		; (11) DC1
;920317 BRA	CNKHF:16		; (12) DC2 Cancel Kanji Half Mode
	BRA	CNCND:16		; (12) DC2 Cancel Half Mode  920317
	BRA	FSERR:16		; (13) DC3
	BRA	CNDB1:16		; (14) DC4 Cancel Double-width Mode <1 Line>
	BRA	FSERR:16		; (15) NAK
	BRA	FSERR:16		; (16) SYN
	BRA	FSERR:16		; (17) ETB
	BRA	FSERR:16		; (18) CAN
	BRA	FSERR:16		; (19) EM
	BRA	FSERR:16		; (1A) SUB
	BRA	FSERR:16		; (1B) ESC ESCAPE Sequence
	BRA	FSERR:16		; (1C) FS  FS Sequence
	BRA	FSERR:16		; (1D) GS
	BRA	FSERR:16		; (1E) RS
	BRA	FSERR:16		; (1F) US
;
	BRA	FSERR:16		; (20) SP
	BRA	KJMSL:16		; (21) ! Kanji Master Select
	BRA	FSERR:16		; (22) "
	BRA	FSERR:16		; (23) #
	BRA	FSERR:16		; (24) $
	BRA	FSERR:16		; (25) %
	BRA	SLKNJ:16		; (26) & Select Kanji-Mode
	BRA	FSERR:16		; (27) '
	BRA	FSERR:16		; (28) (
	BRA	FSERR:16		; (29) )
	BRA	FSERR:16		; (2A) *
	BRA	FSERR:16		; (2B) +
	BRA	FSERR:16		; (2C) ,
	BRA	SLKUL:16		; (2D) - Select Kanji Under Line
	BRA	CNKNJ:16		; (2E) . Cancel Kanji-Mode
	BRA	FSERR:16		; (2F) /
	BRA	FSERR:16		; (30) 0
	BRA	FSERR:16		; (31) 1
	JMP	@DFGAJ			;*(32) 2 Set Gaiji
	BRA	FSERR:16		; (33) 3
	BRA	FSERR:16		; (34) 4
	BRA	FSERR:16		; (35) 5
	BRA	FSERR:16		; (36) 6
	BRA	FSERR:16		; (37) 7
	BRA	FSERR:16		; (38) 8
	BRA	FSERR:16		; (39) 9
	BRA	FSERR:16		; (3A) :
	BRA	FSERR:16		; (3B) ;
	BRA	FSERR:16		; (3C) <
	BRA	FSERR:16		; (3D) =
	BRA	FSERR:16		; (3E) >
	BRA	FSERR:16		; (3F) ?
	BRA	FSERR:16		; (40) @
	BRA	FSERR:16		; (41) A
	BRA	FSERR:16		; (42) B
	BRA	FSERR:16		; (43) C
	BRA	SLHVC:16		; (44) D Kanji Half Vertical Combine
	BRA	FSERR:16		; (45) E
	BRA	FSERR:16		; (46) F
	BRA	FSERR:16		; (47) G
	BRA	FSERR:16		; (48) H
	BRA	FSERR:16		; (49) I
	BRA	SLKJV:16		; (4A) J Select Kanji Vertical Mode
	BRA	CNKJV:16		; (4B) K Cancel Kanji Vertical Mode
	BRA	FSERR:16		; (4C) L
	BRA	HGMBT:16		; (4D) M MBYTE HG PROCESS 920207
	BRA	FSERR:16		; (4E) N
	BRA	FSERR:16		; (4F) O
	BRA	FSERR:16		; (50) P
	BRA	FSERR:16		; (51) Q
	BRA	FSERR:16		; (52) R
	BRA	STKFS:16		; (53) S Set Kanji Full Space
	BRA	STKHS:16		; (54) T Set Kanji Half Space
	BRA	SLKHA:16		; (55) U Select Kanji Half Space Adjust
	BRA	CNKHA:16		; (56) V Cancel Kanji Half Space Adjust
	BRA	SLK4X:16		; (57) W Select Kanji 4X Mode
	BRA	FS58R:16		; (58) X Double height upper/lower 920406
	BRA	FSERR:16		; (59) Y
	BRA	FSERR:16		; (5A) Z
	BRA	FSERR:16		; (5B) [
	BRA	FSERR:16		; (5C) \
	BRA	FSERR:16		; (5D) ]
	BRA	FSERR:16		; (5E) ^
	BRA	FSERR:16		; (5F) _
	BRA	FSERR:16		; (60) `
	BRA	FS13P:16		; (61) a 13.3 | 12 CPI Control 920401
	BRA	FSERR:16		; (62) b
	BRA	FSERR:16		; (63) c
	BRA	FSERR:16		; (64) d
	BRA	FSERR:16		; (65) e
	BRA	FSERR:16		; (66) f
	BRA	FSERR:16		; (67) g
	BRA	FSERR:16		; (68) h
	BRA	FSERR:16		; (69) i
	BRA	FSERR:16		; (6A) j
	BRA	IGN1B:16		; (6B) k Select Kanji Font
	BRA	FSERR:16		; (6C) l
	BRA	FSERR:16		; (6D) m
	BRA	FSERR:16		; (6E) n
	BRA	FSERR:16		; (6F) o
	BRA	FSERR:16		; (70) p
	BRA	FSERR:16		; (71) q
	BRA	SLK14:16		; (72) r Select Kanji 1/4 Mode
	BRA	FSERR:16		; (73) s
	BRA	HGSEL:16		; (74) t HANGUL MODE SELECT 920207
	BRA	FSERR:16		; (75) u
	BRA	HVCON:16		; (76) v KSSM Vertical connection
	BRA	FSERR:16		; (77) w
	BRA	SLKHS:16		; (78) x Select KANJI High-Speed
	BRA	FSERR:16		; (79) y
	BRA	FSERR:16		; (7A) z
	BRA	FSERR:16		; (7B) {
	BRA	FSERR:16		; (7C) |
	BRA	FSERR:16		; (7D) }
	BRA	FSERR:16		; (7E) ~
	BRA	FSERR:16		; (7F) DEL
;
;	FS  + k + n
;
;	IGNORE 1 BYTE
;
IGN1B:	JSR	@GETC			; GET DATA (n)
FSERR:	LDC.B	#0,DP			; RESET < DP >
	RTS
	.PAGE
;
;	13.3 || 12 CPI Control 920401
FS13P:	JSR	@GETC
	AND.B	#H'03,R0
	CMP.B	#H'1,R0
	BEQ	FS13P1
	CMP.B	#H'0,R0
	BEQ	FS13P2
	RTS
;
FS13P1: MOV.B	#H'0,R0
	MOV.B	R0,@ANKSPS:8
	BCLR.B	#QKCSWH,@UMOD01:8	; 12   CPI
	BCLR.B	#QPIT15,@PTMD1F:8
	BSET.B	#QELITE,@PTMD1F:8
	MOV.B	#3,@KFSPLS		; 911127+
	MOV.B	#3,@KFSPRS		; 911127+
	MOV.B	#2,@KHSPLS		; 920326
	MOV.B	#2,@KHSPRS		; 920326
	RTS
;
FS13P2: MOV.B	#H'0,R0
	MOV.B	R0,@ANKSPS:8
	BSET.B	#QKCSWH,@UMOD01:8	; 13.3 CPI
	BCLR.B	#QPIT15,@PTMD1F:8
	BSET.B	#QELITE,@PTMD1F:8
	MOV.B	#1,@KFSPLS		; 911127+
	MOV.B	#2,@KFSPRS		; 911127+
	MOV.B	#1,@KHSPLS		; 920326
	MOV.B	#2,@KHSPRS		; 920326
	RTS
;
;	VERTICAL & HORIZONTAL CONNECTION	920316
HVCON:	JSR	@GETC
	AND.B	#H'03,R0
	CMP.B	#H'1,R0
	BEQ	HVCON1
	CMP.B	#H'0,R0
	BEQ	HVCON2
	RTS
HVCON1: BSET.B	#QKEIVP,@UMOD01:8	;
	RTS
HVCON2: BCLR.B	#QKEIVP,@UMOD01:8	;
	RTS
;
;	<< FUNCTION CODE PROCESS >>
;
;	HANGUL COMBI/COMPOSITION SELLECT       920207
HGSEL:	JSR	@GETC
	AND.B	#H'03,R0
	CMP.B	#H'1,R0
	BEQ	HGSEL1
	CMP.B	#H'0,R0
	BEQ	HGSEL2
	RTS
HGSEL1: BSET.B	#QKCSWD,@UMOD01:8	; COMBINATION
	RTS
HGSEL2: BCLR.B	#QKCSWD,@UMOD01:8	; COMPOSITION
	RTS
;
;	MBYTE HG DELEMITER SET 920207
HGMBT:	JSR	@GETC
	LDC.B	#PAGE HBUF20,EP 	;327+
	MOV.W	#HBUF20,R4		;327+
	MOV.B	R0,@(1,R4)		;327+
	JSR	@GETC
	LDC.B	#PAGE HBUF20,EP 	;327+
	MOV.W	#HBUF20,R4		;327+
	MOV.B	R0,@(2,R4)		;327+
	RTS
;
;	BEL
;
;	BELL
;
BELCD:	TST.W	@BUZT			;
	BMI	BELCD			; BR. IF BUZZER TIMER BUSY
	CALIOS	#BZBEL			; CALL BELL TONE ON PACKET TO I/O SYSTEM
	RTS				;
;
;	BS
;
;	BACK SPACE
;
BSPCD:	BTST.B	#QBCDPR,@PRPTCF:8	;
	BNE	BSPC_6			; BR. IF BAR CODE MODE
	BTST.B	#QCGGRP,@PTMD3F:8	;
	BNE	BSPC_6			; BR. IF PREVIOUS DATA = GRAPHIC (INVALID BS.)
	BTST.B	#QKJMD,@PTMD5F:8	;
	BNE	BSPC_2			; BR. IF KANJI MODE
	BTST.B	#QPROPO,@PTMD1F:8	;
	BEQ	BSPC_2			; BR. IF NOT PROPORTIONAL
	MOV.W	@CHRWDS:8,R2		; GET PREVIOUS DATA WIDTH
	BEQ	BSPC_2			; BR. IF BACK VALUE = PICA/ELITE-CONDENSE PITCH
	CLR.W	@CHRWDS:8		; CLEAR PREVIOUS DATA WIDTH SAVE AREA
	BRA	BSPC_4
	;
BSPC_2: JSR	@GETPC1 		; GET CHARACTER PITCH (= R2)
BSPC_4: MOV.W	@CLMCTC:8,R1		; GET CURRENT COLUMN
	SUB.W	R2,R1			; CALCULATE CURRENT COLUMN - PITCH
	MOV.W	R1,@DATLNS:8		; SAVE IT
	BCS	BSPC_6			; BR. IF UNDERFLOW
	CMP.W	@LMARGS:8,R1		;
	BCS	BSPC_6			; BR. IF OBJECT COLUMN < LEFT MARGIN
	JMP	@NXCLM			; OBJECT COLUMN >= LEFT MARGIN
	;
BSPC_6: RTS
	.PAGE
;
;	HT
;
;	HORIZONTAL TAB
;
HTBCD:	BTST.B	#QHTSTR,@APL0F:8	;
	BNE	HTCD_4			; BR. IF TAB-SET SEQ. RECEIVE
	JSR	@GETPC1 		; GET CHARACTER PITCH (= R2)
	SHLL.W	R2			; x 2
	SHLL.W	R2			; x 4
	SHLL.W	R2			; x 8 (MAKE EACH 8 CHARACTER SIZE)
	MOV.W	@LMARGS:8,R1		; SET BASE = CURRENT LEFT MARGIN
HTCD_2: ADD.W	R2,R1			; BASE + (CHARACTER PITCH) x 8
	CMP.W	@CLMCTC:8,R1		;
	BLS	HTCD_2			; BR. IF CURRENT >= TAB POSITION
	BRA	HTCD_8			; CURRENT < TAB POSITION
	;
HTCD_4: MOV.W	#HTBF,FP		; SET HT TABLE BASE ADDRESS
HTCD_6: TST.W	@FP			; CHECK DATA
	BEQ	BSPC_6			; BR. IF END
	MOV.W	@FP+,R1 		; GET DATA (TAB POSITION)
	ADD.W	@LMARGS:8,R1		; ADJUST CURRENT LEFT MARGIN
	CMP.W	@CLMCTC:8,R1		;
	BLS	HTCD_6			; BR. IF TAB POSITION < CURRENT
HTCD_8: CMP.W	@RMARGS:8,R1		;
	BCC	BSPC_6			; BR. IF TAB POSITION >= RIGHT MARGIN
	MOV.W	R1,@-SP 		;+ SAVE OBJECT COLUMN
	JSR	@GPTPM			; GET CURRENT PITCH
	BTST.B	#QBCDPR,@PRPTCF:8	;
	BEQ	HTCD_9			; BR. IF NOT BAR OCDE MODE
	MOV.W	#ZCL180,R2		; SET BAR CODE PITCH (1/180 INCH)
HTCD_9: SUB.W	@CLMCTC:8,R1		; CALCULATE RELATIVE COLUMN
	BSR	HTCD_A			; GET PITCH COLUMN
	CLR.W	R0			;
	DIVXU.W R2,R0			; CALCULATE ADJUST COLUMN
	SWAP	R0			; GET REMAINDER
	MOV.W	@SP+,R1 		;+ UNSAVE OBJECT COLUMN
	EXTU	R0			;
	SUB.W	R0,R1			; ADJUST OBJECT COLUMN
	MOV.W	R1,@DATLNS:8		; SET OBJECT COLUMN
	JSR	@NXCLM			; SET NEXT COLUMN
	MOV.W	@TXTBFP:8,FP		; SET TEXT BUFFER POINTER
	CMP.W	#TXTBF,FP		;
	BEQ	HTCD_7			; BR. IF NO DATA
	SUBS.B	#ZFMTSZ,FP		; SET BEFORE FORMAT POINTER
	MOV.B	#ZHTBFM,@(YFMATF,FP)	; OVERWRITE FORMAT FLAG (HT)
HTCD_7: RTS
	;
HTCD_A: CMP.B	#ZPTC24,R2		;
	BEQ	HTCD_B			; BR. IF 240 DPI
	CMP.B	#ZPTC18,R2		;
	BEQ	HTCD_C			; BR. IF 180 DPI
	BCC	HTCD_D			; BR. IF 120 DPI
	MOV.W	#1440/360,R2		; SET 1/360 INCH COLUMN
	RTS
	;
HTCD_B: MOV.W	#1440/240,R2		; SET 1/240 INCH COLUMN
	RTS
	;
HTCD_C: MOV.W	#1440/180,R2		; SET 1/180 INCH COLUMN
	RTS
	;
HTCD_D: MOV.W	#1440/120,R2		; SET 1/120 INCH COLUMN
	RTS
	.PAGE
;
;	LF
;
;	LINE FEED
;
ESLFCD: BSR	SETPR1:16		; SET END MARK TO TEXT BUFFER & PRINT
LFCD1:	CMP.B	#9,@UMOD04:8		; 920407 UPGRADE
	BEQ	LINFD			;	"
	JSR	@INBFP			; SET NEXT LINE START = LEFT MARGIN
LINFD:	CMP.B	#8,@UMOD04:8		; 920921 TG
	BNE	LFCD2			;   "
	BCLR.B	#QKJVEL,@PTMD4F:8	; 920921 TG
	BCLR.B	#QVENLR,@PTMD0F:8	;   "
	JSR	@TGXXP			; 921203
LFCD2:	BCLR.B	#QENL1L,@PTMD0F:8	; CLEAR DOUBLE-WIDTH FLAG (1 LINE)
	CALDP3	#ZPLFCD 		; CALL LINE FEED PACKET TO DP3
CHKPEO: BTST.B	#QPEOVM,@PRMDF:8	;
	BEQ	CHKP_4			; BR. IF NOT PE OVERRIDE MODE
CHKP_2: JSR	@CHKMD			; CHECK MODE CHANGE
	JSR	@CHKDP3 		;
	BNE	CHKP_2			; BR. IF NOT PRINT/FEED END
CHKP_4: RTS
;
;	VT
;
;	VERTICAL TAB
;
ESVTCD: BTST.B	#QVTSTR,@APL0F:8	;
	BEQ	ESLFCD			; BR. IF NOT TAB-SET SEQ. RECEIVE (VT = LF)
	MOV.B	@VTCHNS:8,R4		; GET CURRENT CHANNEL
	MOV.W	R4,@-SP 		;+ SAVE CHANNEL
	BSR	SETPR1			; SET END MARK TO TEXT BUFFER & PRINT
	JSR	@INBFP			; SET NEXT LINE START = LEFT MARGIN
	MOV.W	@SP+,R4 		;+ UNSAVE CHANNEL
	BTST.B	R4,@VTCHNF:8		;
	BEQ	CHKPEO			; BR. IF NOT CURRENT CHANNEL VT (VT = CR)
	BCLR.B	#QENL1L,@PTMD0F:8	; CLEAR DOUBLE-WIDTH FLAG (1 LINE)
	CALDP3	#ZPVTCD 		; CALL PERFORM VT PACKET TO DP3
	BRA	CHKPEO
	.PAGE
;
;	FF
;
;	FORM FEED
;
ESFFCD: BSR	SETPR1			; SET END MARK TO TEXT BUFFER & PRINT
	JSR	@INBFP			; SET NEXT LINE START = LEFT MARGIN
	BCLR.B	#QENL1L,@PTMD0F:8	; CLEAR DOUBLE-WIDTH FLAG (1 LINE)
	CMP.B	#8,@UMOD04:8		; 920921 TG
	BNE	FFCD2			;   "
	BCLR.B	#QKJVEL,@PTMD4F:8	;
	BCLR.B	#QVENLR,@PTMD0F:8	;   "
	JSR	@TGXXP			; 921203
FFCD2:
	CALDP3	#ZPFFCD 		; CALL FORM FEED PACKET TO DP3
	BRA	CHKPEO
;
;	CR
;
;	CARRIAGE RETURN
;
CRCD:	BCLR.B	#QKMSKF,@UMOD05:8	; MASKING BLOCK CLEAR
	BSR	SETPR1			; SET END MARK TO TEXT BUFFER & PRINT
	JSR	@INBFP			; SET NEXT LINE START = LEFT MARGIN
	BTST.B	#QATFE,@IFPOTC		;
	BEQ	LINFD			; BR. IF SIGNAL = CR + LF (AUTOFEED XT/)
	BTST.B	#QAUTLF,@EEPBF+E2CPF2	;
	BNE	LINFD			; BR. IF CR + LF (AUTO LF)
	BRA	CHKPEO
;
; << SUB >>	SET END MARK TO TEXT BUFFER & PRINT (CHECK FULL JUSTIFY)
;
;	USE	R0->FP,EP
;
SETPR1: BSET.B	#QPRTCR,@APL2F:8	; SET PRINT ON-LINE FLAG (CR/LF/VT/FF)
STPRNT: BSET.B	#QPRTON,@APL0F:8	; SET PRINT ON-LINE FLAG
SETPRN: BSR	STENDM			; SET END MARK TO TEXT BUFFER
	BEQ	STEN_2			; BR. IF NO DATA
	JSR	@FJUST			; CHECK FULL JUSTIFICATION & PRINT
	RTS
;
STENDM: CMP.W	#TXTBF,@TXTBFP:8	;
	BEQ	STEN_2			; BR. IF NO DATA
	MOV.W	@TXTBFP:8,FP		; SET TEXT BUFFER POINTER
	MOV.W	#ZENDMK,@FP+		; SET END MARK
	MOV.W	@CLMCTC:8,R0		; GET MAX COLUMN (CURRENT COLUMN)
	MOV.W	R0,@FP+ 		; SAVE IT
	CLR.W	@FP+			; CLEAR TEXT BUFFER AREA (+5 -> +9)
	CLR.W	@FP+			;
	CLR.W	@FP+			;
	MOV.W	#01,R1			; SET EXIST DATA FLAG
STEN_2: RTS				;
	;
STEN_4: CLR.W	R1			; SET NO DATA FLAG
	RTS
	.PAGE
;
;	SO
;
;	SELECT DOUBLE-WIDTH MODE (1 LINE)
;
SLDB1:	BSET.B	#QENL1L,@PTMD0F:8	; SET DOUBLE-WIDTH (1 LINE)
	RTS
;
;	SI
;
;	SET CONDENSE MODE
;
STCND:	BSET.B	#QCONDE,@PTMD1F:8	; SET CONDENSE MODE
	BSET.B	#QKJHAF,@PTMD4F:8	; SET KANJI HALF MODE
	BCLR.B	#QKJQUA,@PTMD4F:8	; RESET KANJI 1/4 MODE
	RTS
;
;	CANCEL CONDENSE MODE
;
CNCND:	BCLR.B	#QCONDE,@PTMD1F:8	; RESET CONDENSE MODE
	BCLR.B	#QKJHAF,@PTMD4F:8	; RESET KANJI HALF MODE
	BCLR.B	#QKJQUA,@PTMD4F:8	; RESET KANJI 1/4 MODE
	RTS
	.PAGE
;
;	DC3
;
;	DESELECT PRINTER
; 920131
;DC3CD:  BTST.B  #QSLIN,@IFPOTC 	 ;
;	 BEQ	 DC3C_4 		 ; BR. IF DC1/DC3 IGNORE (SLCTIN/ = LOW)
;DC3C_2: JSR	 @GETC			 ; GET DATA
;	 AND.B	 #ZN-H'80,R0             ; CLEAR MSB
;	 CMP.B	 #ZDC1,R0		 ;
;	 BNE	 DC3C_2 		 ; BR. IF NOT DATA = DC1
;DC3C_4: RTS
;
; 920131
DC3CD:	BSR	SETPR1:16		; SET END MARK TO TEXT BUFFER & PRINT
	JSR	@INBFP			; SET NEXT LINE START = LEFT MARGIN
	MOV.W	#H'0400,R2
	TRAPA	#10
	RTS
;
;	DC4
;
;	CANCEL DOUBLE-WIDTH MODE (1 LINE)
;
CNDB1:	BCLR.B	#QENL1L,@PTMD0F:8	; RESET DOUBLE-WIDTH (1 LINE)
	RTS
;
;	CAN
;
;	CANCEL
;
CANCD:	BTST.B	#QIMGDT,@APL0F:8	;
	BEQ	CANC_2			; BR. IF NOT IMAGE DATA PRESENT
	LDC.B	#PAGE IMGBF,EP		; SET IMAGE BUFFER PAGE
	MOV.W	#IMGBF,R4		; SET IMAGE BUFFER START ADDRESS
	MOV.W	#4896*3,R5		; SET RAM SIZE (IMAGE BUFFER SIZE)
	CALSYS	#CLRBFW 		; CALL CLEAR IMAGE BUFFER RAM PACKET TO SYSTEM
	BCLR.B	#QIMGDT,@APL0F:8	; RESET IMAGE DATA RECEIVE FLAG
CANC_2:
	MOV.W	@STCLMC:8,R1		; GET START COLUMN
	MOV.W	R1,@CLMCTC:8		; SET CURRENT COLUMN AREA
	MOV.W	R1,@CLMMXS:8		; SET COLUMN MAX SAVE AREA
	JSR	@INBFC			; SET NEXT LINE START = CURRENT COLUMN
	RTS
	.PAGE
;
;	DEL
;
;	DELETE
;
DELCD:	BTST.B	#QDELVL,@APL2F:8	;
	BEQ	DELC_6			; BR. IF NOT DELETE VALID
	BTST.B	#QPRFST,@PRMDF:8	;
	BEQ	DELC_6			; BR. IF NO DATA
	BSR	DELC_8			; GET FORMAT DATA
	CMP.B	#ZKNJFM,R0		;
	BLS	DELC_4			; BR. IF ANK/KANJI FORMAT
	CMP.B	#ZCMBFM,R0		;
	BNE	DELC_6			; BR. IF NOT COMBINE FORMAT (BR. IF IMAGE/COLUMN/FEED FORMAT)
	MOV.W	#ZFSPCD,@(YCODEF,FP)	; OVERWRITE KANJI SPACE CODE
	BRA	DELC_5
	;
DELC_4: MOV.W	FP,@TXTBFP:8		; SAVE TEXT BUFFER POINTER
DELC_5: MOV.W	@(YCLMCF,FP),R1 	; GET OLD COLUMN FOR TEXT BUFFER
	MOV.W	R1,@CLMCTC:8		; SAVE IT
	MOV.W	R1,@CLMMXS:8		; SAVE MAX COLUMN (END COLUMN)
	CMP.W	#TXTBF,@TXTBFP:8	;
	BCC	DELC_7			; BR. IF NO LINE START
	BCLR.B	#QPRFST,@PRMDF:8	; RESET PRINT FIRST FLAG (= LINE START)
DELC_6: RTS
	;
DELC_7: JSR	@CKSPDB 		; CHECK SPEED B MODE
	JSR	@SVDTA3 		; CHECK 1 LINE PRINT VARIATION FLAG (ANK)
	JSR	@SVDTK2 		; CHECK 1 LINE PRINT VARIATION FLAG (KANJI)
	JMP	@STPVF			; SET 1 LINE PRINT VARIATION FLAG
	;
DELC_8: MOV.W	@TXTBFP:8,FP		; SET TEXT BUFFER POINTER
	SUBS.B	#ZFMTSZ,FP		; - 1 FORMAT SIZE
	MOV.B	@FP,R0			; GET FORMAT FLAG
	RTS
	.PAGE
;
;	<< FS SEQUENCE PROCESS >>
;
;	FS + SI
;
;	SELECT KANJI HALF MODE
;
SLKHF:	BSET.B	#QKJHAF,@PTMD4F:8	; SET KANJI HALF MODE
	BCLR.B	#QKJQUA,@PTMD4F:8	; RESET KANJI 1/4 MODE
	RTS
;
;	FS + DC2
;
;	CANCEL KANJI HALF & 1/4 MODE
;
CNKHF:	BCLR.B	#QKJHAF,@PTMD4F:8	; RESET KANJI HALF MODE
	BCLR.B	#QKJQUA,@PTMD4F:8	; RESET KANJI 1/4 MODE
	RTS
;
;	FS + ! + n
;
;	KANJI MASTER SELECT
;
KJMSL:	JSR	@GETC			; GET DATA (n)
	MOV.B	@PTMD4F:8,R2		; GET PRINT MODE 4
	AND.B	#XKJFMK,R2		; EXCEPT KANJI FLAG MASK
	MOV.B	R2,@PTMD4F:8		; SAVE IT
	BCLR.B	#QHENLR,@PTMD0F:8	; RESET HORIZONTAL ENLARGE
	BCLR.B	#QENL1L,@PTMD0F:8	; RESET HORIZONTAL ENLARGE (1 LINE)
	BTST.B	#4,R0			;
	BEQ	KJMS_2			; BR. IF NOT 1/4 SELECT
	BCLR.B	#1,R0			; RESET HALF SELECT
KJMS_2: BTST.B	#2,R0			;
	BEQ	KJMS_4			; BR. IF NOT ENLARGE
	BSET.B	#QHENLR,@PTMD0F:8	; SET HORIZONTAL ENLARGE
	BCLR.B	#2,R0			;
KJMS_4: OR.B	@PTMD4F:8,R0		; OR WITH OLD FLAG
	MOV.B	R0,@PTMD4F:8		; SAVE IT
	RTS
	.PAGE
;
;	FS + &
;
;	SELECT KANJI MODE
;
SLKNJ:	BSET.B	#QKJMD,@PTMD5F:8	; SET KANJI MODE
	RTS
;
;	FS + - + n
;
;	SELECT KANJI UNDERLINE MODE
;
SLKUL:	JSR	@GETC			; GET DATA (n)
	TST.B	R0			;
	BMI	SLKU_2			; BR. IF ERROR DATA
	CMP.B	#3,R0			;
	BCC	SLKU_2			; BR. IF ERROR DATA
	BCLR.B	#QKJULN,@PTMD4F:8	; RESET KANJI UNDERLINE
	TST.B	R0			;
	BEQ	SLKU_2			; BR. IF n = 0 (RESET KANJI UNDERLINE MODE)
	BSET.B	#QKJULN,@PTMD4F:8	; SET KANJI UNDERLINE MODE
	BCLR.B	#QKJ2UL,@PTMD4F:8	; RESET 2 DOTS UNDERLINE
	BTST.B	#0,R0			;
	BNE	SLKU_2			; BR. IF n = 1 (1 DOT UNDERLINE)
	BSET.B	#QKJ2UL,@PTMD4F:8	; SET 2 DOTS UNDERLINE
SLKU_2: RTS
;
;	FS + .
;
;	CANCEL KANJI MODE
;
CNKNJ:	BCLR.B	#QKJMD,@PTMD5F:8	; RESET KANJI MODE
	RTS
;
;	FS + D
;
;	SELECT KANJI HALF VERTICAL COMBINE MODE
;
SLHVC:	BTST.B	#QKJMD,@PTMD5F:8	;
	BEQ	SLKU_2			; BR. IF NOT KANJI MODE
	BTST.B	#QKJVRT,@PTMD4F:8	;
	BEQ	SLKU_2			; BR. IF NOT KANJI VERTICAL
	BSET.B	#QKJCBM,@PTMD5F:8	; SET KANJI COMBINE MODE
	BCLR.B	#QKJCB2,@PTMD5F:8	; RESET KANJI COMBINE 2ND MODE
	RTS
	.PAGE
;
;	FS + J
;
;	SELECT KANJI VERTICAL MODE
;
SLKJV:	BSET.B	#QKJVRT,@PTMD4F:8	; SET KANJI VERTICAL MODE
	RTS
;
;	FS + K
;
;	CANCEL KANJI VERTICAL MODE
;
CNKJV:	BCLR.B	#QKJVRT,@PTMD4F:8	; RESET KANJI VERTICAL MODE
	BTST.B	#QKJCB2,@PTMD5F:8	;
	BNE	CNKJ_2			; BR. IF COMBINE 2ND
	BCLR.B	#QKJCBM,@PTMD5F:8	; RESET KANJI COMBINE MODE
CNKJ_2: RTS
	RTS
;
;	FS + S + n1 + n2
;
;	SET KANJI FULL SPACE
;
STKFS:	JSR	@GETC			; GET DATA (n1)
	AND.B	#ZN-H'80,R0             ; CLEAR MSB
	MOV.B	R0,@KFSPLS:8		; SET KANJI LEFT SPACE
	JSR	@GETC			; GET DATA (n2)
	AND.B	#ZN-H'80,R0             ; CLEAR MSB
	MOV.B	R0,@KFSPRS:8		; SET KANJI RIGHT SPACE
	BCLR.B	#QKCSWH,@UMOD01:8	; 13.3 CPI Clear
	RTS
	.PAGE
;
;	FS + T + n1 + n2
;
;	SET KANJI HALF SPACE
;
STKHS:	JSR	@GETC
	AND.B	#H'0F,R0
	MOV.B	R0,@ORGDAT:8
	JSR	@GETC
	AND.B	#H'0F,R0
	ADDS.B	@ORGDAT:8,R0
STKSS:	CMP.B	#H'0,R0
	BEQ	STKH10
	CMP.B	#H'1,R0
	BEQ	STKH11
	CMP.B	#H'2,R0
	BEQ	STKH12
	CMP.B	#H'3,R0
	BEQ	STKH20
	CMP.B	#H'4,R0
	BEQ	STKH21
	CMP.B	#H'5,R0
	BEQ	STKH22
	CMP.B	#H'6,R0
	BEQ	STKH30
	CMP.B	#H'7,R0
	BEQ	STKH31
	CMP.B	#H'8,R0
	BEQ	STKH32
	CMP.B	#H'9,R0
	BEQ	STKH33
	CMP.B	#H'A,R0
	BEQ	STKH34
	CMP.B	#H'B,R0
	BEQ	STKH35
	CMP.B	#H'C,R0
	BEQ	STKH36
	CMP.B	#H'D,R0
	BEQ	STKH37
	CMP.B	#H'E,R0
	BEQ	STKH38
	CMP.B	#H'F,R0
	BEQ	STKH39
	RTS
	.PAGE
;
STKH10: MOV.B	#H'0,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH1F
;
STKH11: MOV.B	#H'1,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH1F
;
STKH12: MOV.B	#H'2,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH1F
;
STKH1F: BSET.B	#QPIT15,@PTMD1F:8
	BCLR.B	#QELITE,@PTMD1F:8
	RTS
;
STKH20: MOV.B	#H'0,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH2F
;
STKH21: MOV.B	#H'1,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH2F
;
STKH22: MOV.B	#H'2,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH2F
;
STKH2F: BCLR.B	#QPIT15,@PTMD1F:8
	BSET.B	#QELITE,@PTMD1F:8
	RTS
;
STKH30: MOV.B	#H'0,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH31: MOV.B	#H'1,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH32: MOV.B	#H'2,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH33: MOV.B	#H'3,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH34: MOV.B	#H'4,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH35: MOV.B	#H'5,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH36: MOV.B	#H'6,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH37: MOV.B	#H'7,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH38: MOV.B	#H'8,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH39: MOV.B	#H'9,R0
	MOV.B	R0,@ANKSPS:8
	BRA	STKH3F
;
STKH3F: BCLR.B	#QPIT15,@PTMD1F:8
	BCLR.B	#QELITE,@PTMD1F:8
	RTS
	.PAGE
;
;	FS + U
;
;	SELECT KANJI HALF SPACE ADJUST
;
;SLKHA:  BSET.B  #QKJHAJ,@PTMD4F:8	 ; SET KANJI HALF SPACE ADJUST
;	 RTS
;
SLKHA:	MOV.B	@KFSPLS:8,R0		; 930417
	MOV.B	R0,@ORGDAT:8
	MOV.B	@KFSPRS:8,R0
	ADDS.B	@ORGDAT:8,R0
	SHLR.B	R0
	BRA	STKSS
;
;
;	FS + V
;
;	CANCEL KANJI HALF SPACE ADJUST
;
;CNKHA:  BCLR.B  #QKJHAJ,@PTMD4F:8	 ; RESET KANJI HALF SPACE ADJUST
;	 BSET.B  #QKJHAC,@APL2F:8	 ; SET KANJI HALF ADJUST TIMING FLAG (1ST)
;	 RTS
CNKHA:	RTS
	.PAGE
;
;	FS + W + n
;
;	SELECT KANJI x 4 MODE
;
SLK4X:	BSR	CKDT01			; GET & CHECK DATA = 0 or 1
	BNE	SLK4_2			; BR. IF ERROR DATA
	BCLR.B	#QKJVEL,@PTMD4F:8	; RESET KANJI VERTICAL ENLARGE
	BCLR.B	#QHENLR,@PTMD0F:8	; RESET HORIZONTAL ENLARGE
	BCLR.B	#QENL1L,@PTMD0F:8	; RESET HORIZONTAL ENLARGE (1 LINE)
	BCLR.B	#QVENLR,@PTMD0F:8	; SET DOUBLE HIGHT FLAG 영문
	TST.B	R0			;
	BEQ	SLK4_2			; BR. IF n = 0 (RESET KANJI x4 MODE
	BSET.B	#QKJVEL,@PTMD4F:8	; SET KANJI VERTICAL ENLARGE
	BSET.B	#QHENLR,@PTMD0F:8	; SET HORIZONTAL ENLARGE
	BSET.B	#QVENLR,@PTMD0F:8	; SET DOUBLE HIGHT FLAG 영문
SLK4_2: RTS
;
;	FS + r + n
;
;	SELECT KANJI 1/4 MODE
;
SLK14:	BSR	CKDT01			; GET & CHECK DATA = 0 or 1
	BNE	SLK4_2			; BR. IF ERROR DATA
	BSET.B	#QKJQUA,@PTMD4F:8	; SET KANJI 1/4 MODE
	BCLR.B	#QKJQLW,@PTMD4F:8	; RESET KANJI 1/4 SUBSCRIPT MODE (SET KANJI 1/4 SUPERSCRIPT)
	TST.B	R0			;
	BEQ	SLK4_2			; BR. IF n = 0 (SET KANJI SUBSCRIPT)
	BSET.B	#QKJQLW,@PTMD4F:8	; SET KANJI 1/4 SUBSCRIPT MODE
	RTS
;
;	FS + x + n
;
;	SELECT KANJI HIGH SPEED (-> VALID LQ or IMAGE MODE < SAME PITCH >)
;
SLKHS:	BSR	CKDT01			; GET & CHECK DATA = 0 or 1
	BNE	SLK4_2			; BR. IF ERROR DATA
	BCLR.B	#QKHSMD,@PTMD5F:8	; RESET KANJI HIGH SPEED
	TST.B	R0			;
	BEQ	SLK4_2			; BR. IF n = 0 (RESET HIGH SPEED MODE)
	BSET.B	#QKHSMD,@PTMD5F:8	; SET KANJI HIGH SPEED
	RTS
	.PAGE
;
; << SUB >>	SET LINE START MODE
;
;	USE	R1->R2,R5
;
STLSM:	BSET.B	#QPRFST,@PRMDF:8	; SET PRINT FIRST FLAG (LINE START)
	MOV.B	@PTMD3F:8,R1		; GET PRINT MODE 3 FLAG
	AND.B	#XCOLMK,R1		; MASK EXCEPT CURRENT COLOR
	MOV.B	R1,@PTMD3F:8		; SAVE IT
	RTS
;
; << SUB >>	SET PRINT VARIATION FLAG
;
;	IN	FP : TEXT BUFFER POINTER
;
;	USE	R2->R5
;
STPVF:	BTST.B	#QIMGDT,@APL0F:8	;
	BNE	STPV_3			; BR. IF NOT IMAGE DATA PRESENT
	BTST.B	#QDBSTR,@PTMD1F:8	;
	BEQ	STPV_3			; BR. IF NO DOUBLE STRIKE
	BSET.B	#QIMGPR,@PRMODF:8	; SET IMAGE DATA PRINT REQUEST
STPV_3:
STPV_0: MOV.B	@PTMD3F:8,R2		; GET PRINT MODE 3
	AND.B	#XCOLMK,R2		; MASK EXCEPT COLOR FLAG
STPV_1: CMP.B	@COLORF:8,R2		;
	BEQ	STPV_4			; BR. IF SAME COLOR BIT
	CMP.W	#TXTBF,@TXTBFP:8	;
	BNE	STPV_2			; BR. IF DATA INPUT
	MOV.B	@COLORF:8,R3		;
	AND.B	#XCP7P,R3		;
	MOV.B	R3,@COLORF:8		;
STPV_2: OR.B	@COLORF:8,R2		; OR WITH OLD COLOR TO CURRENT COLOR
	MOV.B	R2,@COLORF:8		; SAVE NEW COLOR FLAG
STPV_4: CMP.W	#TXTBF,FP		;
	BEQ	STPV_5			; BR. IF NO DATA
	MOV.W	FP,@-SP 		;+ SAVE TEXT BUFFER POINTER (CURRENT)
	SUBS.B	#ZFMTSZ,FP		; ADJUST TEXT BUFFER POINTER (BEFORE)
	MOV.B	@(YCOLRF,FP),R2 	; GET OLD FORMAT COLOR FLAG
	AND.B	#H'0F,R2                ; MASK EXCEPT COLOR FLAG
	MOV.W	@SP+,FP 		;+ UNSAVE TEXT BUFFER POINTER (CURRENT)
	MOV.B	@(YCOLRF,FP),R3 	; GET CURRENT FORMAT COLOR FLAG
	AND.B	#H'0F,R3                ; MASK EXCEPT COLOR FLAG
	CMP.B	R3,R2			;
	BEQ	STPV_5			; BR. IF SAME COLOR (OLD = CURRENT)
	BSET.B	#QCLMLT,@COLORF:8	; SET COLOR MULTI FLAG
STPV_5: BTST.B	#QPRUDI,@PTMD2F:8	;
	BEQ	STPV_6			; BR. IF NOT UNI-DIRECTION MODE
	BSET.B	#QUDPMD,@PRPTCF:8	; SET PRINT UNI-DIRECTION MODE FLAG
STPV_6: CMP.W	#TXTBF,FP		;
	BEQ	STPV_9			; BR. IF NO DATA
	BTST.B	#QCP7P,@COLORF:8	;
	BNE	STPV_9			; BR. IF PANEL COPY MODE
	SUBS.B	#ZFMTSZ,FP		; ADJUST TEXT BUFFER POINTER (BEFORE)
	BTST.B	#QDBSTR,@PTMD1F:8	;
	BEQ	STPV_7			; BR. IF NOT DOUBLE-STRIKE MODE
	BSET.B	#QDBSTP,@PRMODF:8	; SET EXIST DOUBLE-STRIKE FLAG
	BTST.B	#7,@(YCOLRF,FP) 	;
	BNE	STPV_9			; BR. IF BEFORE DOUBLE-STRIKE MODE
	BRA	STPV_8
	;
STPV_7: BTST.B	#7,@(YCOLRF,FP) 	;
	BEQ	STPV_9			; BR. IF NOT BEFORE DOUBLE-STRIKE MODE
STPV_8: BSET.B	#QDBST2,@PRMODF:8	; SET DOUBLE-STRIKE MULTI FLAG
STPV_A: ADDS.B	#ZFMTSZ,FP		; ADJUST TEXT BUFFER POINTER (CURRENT)
STPV_9: RTS
	.PAGE
;
; << SUB >>	GET SPEED/PITCH PARAMETER
;
;	IN	R0 : CODE
;
;	OUT	R1 : SPEED/PITCH PARAMETER
;			SPEED FLAG = LOW
;			PITCH FLAG = HIGH
;
;	USE	R0->R5,EP
;
GTSPD:	JSR	@CKSPDB 		; CHECK SPEED B MODE
	MOV.W	R0,@-SP 		;+ SAVE CODE
	JSR	@GPTPM			; GET PICTH PARAMETER (= R2)
	BSET.B	R2,@PRPTCF:8		; SET PRINT PITCH FLAG (1 LINE)
	MOV.W	@SP+,R0 		;+ UNSAVE CODE
	SHLL.B	R2			; SHIFT 4 TIMES (MAKE HIGH NIBBLE)
	SHLL.B	R2			;
	SHLL.B	R2			;
	SHLL.B	R2			;
	MOV.B	#Z10ASP,R1		; SET KANJI SPEED FLAG
;
	BTST.B	#QKCSWA,@UMOD01:8	; +0609
	BEQ	GTSP_4			; +0609
GTSP_0: MOV.B	#Z30SPD,R1		; SET DRAFT SPEED FLAG
	BTST.B	#QPROPO,@PTMD1F:8	;
	BNE	GTSP_2			; BR. IF PROPORTIONAL MODE
	BTST.B	#QLQMD,@PTMD2F:8	;
	BEQ	GTSP_B			; BR. IF NOT LQ MODE
GTSP_2: MOV.B	#Z10BSP,R1		; SET LQ SPEED FLAG
	BTST.B	#QGRPTB,@APL0F:8	;
	BEQ	GTSP_4			; BR IF NOT GRAPHIC TABLE
	BTST.W	#QAKCDH,R0		;
	BEQ	GTSP_4			; BR. IF STANDARD/KATAKANA CODE
	TST.B	R0			;
	BMI	GTSP_4			; BR. IF DOWNLOAD CODE
	CMP.B	#H'5B,R0                ;
	BCS	GTSP_4			; BR IF H'100->H'15A CODE
	CMP.B	#H'5F,R0                ;
	BHI	GTSP_4			; BR IF H'160->H'17F CODE
	MOV.B	#Z10DSP,R1		; SET LQ SPEED (130 cps) FLAG
GTSP_4: BTST.B	#QKNJHS,@PSPDFS:8	;
	BEQ	GTSP_6			; BR. IF NOT KANJI HIGH SPEED MODE
	MOV.B	@PSPDFS:8,R3		; GET PANEL SPEED
	BCLR.B	#QKNJHS,R3		; RESET KANJI HIGH SPEED FLAG
	CMP.B	#ZSPD15,R3		;
	BCC	GTSP_C			; BR. IF PANEL SPEED >= LOW SPEED
	MOV.B	#Z20SPD,R3		; SET KANJI HIGH SPEED FLAG
	BRA	GTSP_F
	;
GTSP_6: MOV.B	@PSPDFS:8,R3		; GET PANEL SPEED SPEED FLAG
	BCLR.B	#QKNJHS,R3		; RESET KANJI HIGH SPEED FLAG
	CMP.B	#ZSPD10,R3		;
	BNE	GTSP_C			; BR. IF NOT PANEL = LOW SPEED
	MOV.B	#Z10SPD,R3		; SET ALTER SPEED FLAG
	CMP.B	#Z10DSP,R1		;
	BEQ	GTSP_G			; BR. IF SPEED 1.0-D (130 cps) MODE
	BTST.B	#QSPDB,@APL2F:8 	;
	BNE	GTSP_F			; BR. IF EDITING MODE
	BTST.B	#QKCSWA,@UMOD01:8	; +0609
	BEQ	GTSP_A			; +0609 IF KANJI
GTSP_61:
	BTST.B	#QDLSEL,@PTMD0F:8	;
	BEQ	GTSP_G			; BR. IF NOT SELECT DOWNLOAD
	TST.B	R0			;
	BPL	GTSP_G			; BR. IF STANDARD / GRAPHIC CODE
	BTST.W	#QAKCDH,R0		;
	BEQ	GTSP_G			; BR. IF KATAKANA CODE
	BRA	GTSP_F			; BR. DOWNLOAD SPEED FLAG
	;
GTSP_B: BTST.B	#QPIT15,@PTMD1F:8	;
	BNE	GTSP_G			; BR IF 15 CPI MODE
	BTST.B	#QCONDE,@PTMD1F:8	;
	BEQ	GTSP_G			; BR IF NOT CONDENSE MODE
	MOV.B	#Z15SPD,R1		; SET DRAFT CONDENSE SPEED FLAG
	BRA	GTSP_G			;
	;
GTSP_C: ADDS.B	#4,R3			; ADJUST PANEL SPEED
	BRA	GTSP_F
	;
GTSP_A: CMP.B	#ZGJCD,@KJCDS1:8	;
	BNE	GTSP_G			; BR. IF NOT GAIJI CODE
GTSP_F: MOV.B	R3,R1			; EXCHANGE SPEED FLAG
GTSP_G: OR.B	R2,R1			; SET SPEED / PITCH FLAG
	RTS
;
; << SUB >>	CHECK PANEL xN SIZE
;
;	OUT	R2 : = 0 ... PANEL ENLARGE = NORMAL
;
;	USE	R2
;
CHKPXN: CMP.B	#1,@PNLELS:8		;
	BEQ	CKPL_2			; BR. IF xN SIZE = NORMAL
	MOV.W	#01,R2			; SET NOT PANEL ENLARGE NORMAL FLAG
	RTS
	;
CKPL_2: CLR.W	R2			; SET PANEL ENLARGE NORMAL FLAG
	RTS
	.PAGE
;
; << SUB >>	SAVE ANK FORMAT TO TEXT BUFFER
;
;	IN	R0 : ANK CODE
;		R2 : CHARACTER PITCH
;
SVANK:	MOV.W	R0,@-SP 		;+ SAVE ANK CODE
	MOV.W	R2,@-SP 		;+ SAVE CHARACTER PITCH
	BSR	CKTXTB:16		; CHECK TEXT BUFFER POINTER
	MOV.B	#ZANKFM,R2		; SET ANK FORMAT FLAG
	MOV.W	@SP+,R1 		;+ UNSAVE CHARACTER PITCH
	BSR	STCOMN:16		; SET COMMON MODE TO TEXT BUFFER
	MOV.W	@SP+,R0 		;+ UNSAVE ANK CODE
	BSR	GTASPC			; GET DOWNLOAD PRE. SPACE (= R1)
	MOV.B	R1,@(YPRESP,FP) 	; SAVE PRE. SPACE
	MOV.W	R0,@(YCODEF,FP) 	; SAVE ANK CODE TO TEXT BUFFER
	BSR	GTSPD			; GET SPEED/PITCH PARAMETER
	MOV.B	R1,@(YSPDPF,FP) 	; SET SPEED/PITCH PARAMETER
	BSR	GTAMD			; GET ANK MODE FLAG (= R0->R1)
SVANK2: MOV.B	R0,@(YMODEF+1,FP)	; SAVE MODE LOW
	MOV.B	R1,@(YMODEF,FP) 	; SAVE MODE HIGH
	MOV.B	R2,@(YBCKSP,FP) 	;\SET REAR SPACE (04/MAR/1992)
	MOV.B	@UMOD05:8,R2		;\ 920309 11BIT
	MOV.B	R2,@(YMODKF,FP) 	;\ 920309 11BIT
	BSR	STPVF			; SET PRINT VARIATION FLAG
	JMP	@UPTBFP 		; UPDATE TEXT BUFFER POINTER
;
; << SUB >>	GET ANK PRE SPACE SIZE
;
;	IN	R0 : ANK CODE
;
;	OUT	R0 : ANK CODE
;		R1 : PRE. SPACE SIZE (EFFECT ENLARGE)
;
;	USE	R0->R5
;
GTASPC: CLR.B	R1			; CLEAR PRE. SPACE VALUE
	BTST.B	#QDLSEL,@PTMD0F:8	;
	BEQ	GTAS_6			; BR. IF NOT DOWNLOAD MODE
	TST.B	R0			;
	BPL	GTAS_6			; BR. IF STANDARD / GRAPHIC CODE
	BTST.W	#QAKCDH,R0		;
	BEQ	GTAS_6			; BR. IF KATAKANA CODE
	PUSHEP				;+ SAVE EP
	MOV.B	R0,R4			; SAVE DOWNLOAD CODE
	AND.B	#ZN-H'80,R4             ; CLEAR MSB
	MULXU.B #ZDLBLK,R4		; CALCULATE OFFSET
	ADD.W	#DLDBF,R4		; + BASE ADDRESS
	LDC.B	#PAGE DLDBF,EP		; SET PAGE
	MOV.B	@R4,R1			; GET DOWNLOAD PRE. SPACE
	POPEP				;+ UNSAVE EP
	BTST.B	#QHENLR,@PTMD0F:8	;
	BNE	GTAS_4			; BR. IF HORIZONTAL ENALRGE
	BTST.B	#QENL1L,@PTMD0F:8	;
	BEQ	GTAS_6			; BR. IF NOT HORIZONTAL ENALRGE (1 LINE)
GTAS_4: SHLL.B	R1			; PRE. SPACE x 2
GTAS_6: RTS
;
; << SUB >>	GET ANK LOAD MODE PARAMETER
;
;	IN	R0 : ANK CODE
;
;	OUT	R0 : LOAD MODE LOW
;		R1 : LOAD MODE HIGH
;
;	USE	R0->R1
;
GTAMD:	MOV.B	@PTMD1F:8,R0		; GET PRINT MODE 1
	AND.B	#ZN-XDBSTR,R0		; MASK DOUBLE-STRIKE FLAG
	BTST.B	#QLQMD,@PTMD2F:8	;
	BEQ	GTAM_2			; BR. IF NOT LQ MODE
	BSET.B	#QLQMD,R0		; SET LQ MODE FLAG
GTAM_2: MOV.B	@PTMD0F:8,R1		; GET PRINT MODE 0
	AND.B	#ZN-XDLSEL-XENL1L,R1	; MASK HORIZONTAL ENLARGE (1 LINE) & DOWNLOAD FLAG
	BTST.B	#QENL1L,@PTMD0F:8	;
	BEQ	GTAM_4			; BR. IF NOT HORIZONTAL ENLARGE (1 LINE)
	BSET.B	#QHENLR,R1		; SET HORIZONTAL ENLARGE (1 LINE) MODE
GTAM_4: BSR	CHKPXN			; CHECK PANEL xN SIZE
	BEQ	GTAM_6			; BR. IF PANEL xN SIZE = NORMAL
	BCLR.B	#QVENLR,R1		; RESET VERTICAL ENLARGE FOR ANK
GTAM_6: RTS
	.PAGE
;
; << SUB >>	SAVE KANJI FORMAT TO TEXT BUFFER
;
;	IN	R0 : KANJI CODE
;		R2 : CHARACTER PITCH
;
SVKNJ:	MOV.W	R0,@-SP 		;+ SAVE KANJI CODE
	MOV.W	R2,@-SP 		;+ SAVE CHARACTER PITCH
	BSR	CKTXTB:16		; CHECK TEXT BUFFER POINTER
	MOV.B	#ZKNJFM,R2		; SET KANJI FORMAT FLAG
SVKNJ2: MOV.W	@SP+,R1 		;+ UNSAVE CHARACTER PITCH
	BSR	STCOMN:16		; SET COMMON MODE TO TEXT BUFFER
	BSR	GTKSPC			; GET KANJI PRE. SPACE (= R1)
	MOV.B	R1,@(YPRESP,FP) 	; SAVE PRE. SPACE
	MOV.W	@SP+,R0 		;+ UNSAVE KANJI CODE
	MOV.W	R0,@(YCODEF,FP) 	; SAVE KANJI CODE TO TEXT BUFFER
	BSR	GTSPD			; GET SPEED/PITCH PARAMETER
	MOV.B	R1,@(YSPDPF,FP) 	; SET SPEED/PITCH PARAMETER
	BSR	GTKMD			; GET KANJI MODE FLAG (= R0->R1)
;920312 CONNECT
	BSR	GTKSPC2 		;\GET BACK SPACE VALUE
;END
	BRA	SVANK2
;
;---------------------------------------
; 920312 CONNECT
; << SUB >>	GET KANJI BACK SPACE SIZE
;
;	OUT	R2 : BACK SPACE SIZE (EFFECT ENLARGE)
;
GTKSPC2:
	BTST.B	#QKCSWG,@UMOD01:8	; 920331 10 CPI HG
	BNE	GTKS_X			;	"
	MOV.B	@KHSPRS:8,R2		;\GET HALF LEFT SPACE VALUE
	BTST.B	#QKJVRT,@PTMD4F:8	;\
	BNE	GTKS_A			;\BR. IF VERTICAL FORMAT
	BTST.B	#QKJHAF,@PTMD4F:8	;\
	BNE	GTKS_C			;\BR. IF HALF MODE
GTKS_A: BTST.B	#QKJQUA,@PTMD4F:8	;\
	BNE	GTKS_C			;\BR. IF 1/4 MODE
	MOV.B	@KFSPRS:8,R2		;\GET FULL LEFT SPACE VALUE
GTKS_C: BTST.B	#QHENLR,@PTMD0F:8	;\
	BNE	GTKS_E			;\BR. IF HORIZONTAL ENALRGE
	BTST.B	#RHGDW,@UMOD06:8	;930129
	BNE	GTKS_E			;  "
	BTST.B	#QENL1L,@PTMD0F:8	;\
	BEQ	GTKS_G			;\BR. IF NOT HORIZONTAL ENALRGE (1 LINE)
GTKS_E: SHLL.B	R2			;\PRE. SPACE x 2
GTKS_G: RTS
;
GTKS_X: MOV.B	#4,R2			; 920331 10 CPI HG
	RTS				;	"
;
;----------------------------------------
;
; << SUB >>	GET KANJI PRE SPACE SIZE
;
;	OUT	R1 : PRE. SPACE SIZE (EFFECT ENLARGE)
;
GTKSPC: BTST.B	#QKCSWG,@UMOD01:8	; 920331 10 CPI HG
	BNE	GTKS_Y			;	"
	MOV.B	@KHSPLS:8,R1		; GET HALF LEFT SPACE VALUE
	BTST.B	#QKJVRT,@PTMD4F:8	;
	BNE	GTKS_2			; BR. IF VERTICAL FORMAT
	BTST.B	#QKJHAF,@PTMD4F:8	;
	BNE	GTKS_4			; BR. IF HALF MODE
GTKS_2: BTST.B	#QKJQUA,@PTMD4F:8	;
	BNE	GTKS_4			; BR. IF 1/4 MODE
	MOV.B	@KFSPLS:8,R1		; GET FULL LEFT SPACE VALUE
GTKS_4: BTST.B	#QHENLR,@PTMD0F:8	;
	BNE	GTKS_6			; BR. IF HORIZONTAL ENALRGE
	BTST.B	#RHGDW,@UMOD06:8	;930129
	BNE	GTKS_6			;  "
	BTST.B	#QENL1L,@PTMD0F:8	;
	BEQ	GTKS_8			; BR. IF NOT HORIZONTAL ENALRGE (1 LINE)
GTKS_6: SHLL.B	R1			; PRE. SPACE x 2
GTKS_8: RTS
;
GTKS_Y: MOV.B	#3,R1			; 920331 10 CPI HG
	RTS				;	"
;
; << SUB >>	GET KANJI LOAD MODE PARAMETER
;
;	OUT	R0 : LOAD MODE LOW
;		R1 : LOAD MODE HIGH
;
;	USE	R0->R1
;
GTKM_X: BSET.B	#QKJHAF,R0		; 920331 10 CPI HG
	BRA	GTKM_Y			;      "
;
GTKMD:	MOV.B	@PTMD4F:8,R0		; GET PRINT MODE 4
	BTST.B	#QKCSWG,@UMOD01:8	; 920331 10 CPI HG
	BNE	GTKM_X			;	"
GTKM_Y:
	AND.B	#ZN-XKJVEL-XKJHAJ,R0	; MASK V-ENLARGE & ADJUST DOT FLAG
	BTST.B	#QEMPHA,@PTMD1F:8	;
	BEQ	GTKM_2			; BR. IF NOT EMPHASIZE MODE
	BSET.B	#QEMPHA,R0		; SET EMPHASIZE MODE
GTKM_2: BTST.B	#QITALC,@PTMD1F:8	;
	BEQ	GTKM_4			; BR. IF NOT ITALIC MODE
	BSET.B	#QITALC,R0		; SET ITALIC MODE
GTKM_4: MOV.B	@PTMD0F:8,R1		; GET PTINT MODE 0
	AND.B	#XOUTLN+XSHADW+XHENLR,R1 ; GET STYLE & HORIZONTAL ENLARGE FLAG
	BTST.B	#QKJVEL,@PTMD4F:8	;
	BEQ	GTKM_5			; BR. IF NOT KANJI VERTIVAL ENLARGE
	BSET.B	#QVENLR,R1		; SET KANJI VERTICAL ENLARGE FLAG
GTKM_5: BTST.B	#RHGDW,@UMOD06:8	;930129
	BNE	GTKM51			;  "
	BTST.B	#QENL1L,@PTMD0F:8	;
	BEQ	GTKM_6			; BR. IF NOT HORIZONTAL ENLARGE (1 LINE)
GTKM51: BSET.B	#QHENLR,R1		; SET HORIZONTAL ENLARGE (1 LINE) MODE
GTKM_6: BSR	CHKPXN			; CHECK PANEL xN SIZE
	BEQ	GTKM_8			; BR. IF PANEL xN SIZE = NORMAL
	BCLR.B	#QVENLR,R1		; RESET VERTICAL ENLARGE FOR ANK
GTKM_8: BTST.B	#QKJCBM,@PTMD5F:8	;
	BEQ	GTKM_A			; BR. IF NOT COMBINE MODE
	BTST.B	#QKJCB2,@PTMD5F:8	;
	BEQ	GTKM_A			; BR. IF COMBINE 1ST CODE
	BSET.B	#QKJCB2,R1		; SET COMBINE 2ND CODE MODE
GTKM_A: BCLR.B	#1,R1			; RESET KEISEN SETSUZOKU
;920316 BTST.B	#QKEIVP,@UMOD01:8	;\ H connection default
;920316 BEQ	GTKM_Z			; BR. IF NOT KEISEN SETSUZOKU
	CMP.B	#H'D4,@(YCODEF,FP)      ;+920415 D4-SEMI connection
	BEQ	GTKM_Q			;+920414
	CMP.B	#H'26,@(YCODEF,FP)      ;
	BNE	GTKM_Z			; BR IF NOT KEISEN CODE
	BSET.B	#1,R1			; SET KEISEN SETSUZOKU
GTKM_Z: RTS
;
GTKM_Q: MOV.W	R3,@-SP 		;+920415 D4-SEMI CONNECTION
	MOV.W	@(YCODEF,FP),R3
	SWAP	R3
	CMP.B	#H'B3,R3
	BCC	GTKMQ1
GTKMQ0: MOV.W	@SP+,R3
	RTS
GTKMQ1: CMP.B	#H'DA,R3
	BCC	GTKMQ0
	MOV.W	@SP+,R3
	BSET.B	#1,R1
	RTS
;
; << SUB >>	SAVE KANJI COMBINE FORMAT TO TEXT BUFFER
;
;	IN	R0 : KANJI CODE
;		R2 : CHARACTER PITCH
;
SVKCM:	MOV.W	R0,@-SP 		;+ SAVE KANJI COMBINE CODE
	MOV.W	R2,@-SP 		;+ SAVE CHARACTER PITCH
	BSR	CKTXTB:16		; CHECK TEXT BUFFER POINTER
	MOV.B	#ZCMBFM,R2		; SET KANJI FORMAT FLAG
	BRA	SVKNJ2			; UPDATE TEXT BUFFER POINTER
	.PAGE
;
; << SUB >>	SAVE COLUMN COMMAND FORMAT TO TEXT BUFFER
;
;	IN	DATLNS : RELATIVE COLUMN
;		    R5 : FUNCTION CODE (DIRECTION FLAG)
;
SVCCM:	MOV.W	@DATLNS:8,R0		; GET RELATIVE COLUMN
	MOV.W	R0,@-SP 		;+ SAVE RELATIVE COLUMN
	MOV.W	R5,@-SP 		;+ SAVE FUNCTION CODE (DIRECTION FLAG)
	BSR	CKTXTB:16		; CHECK TEXT BUFFER POINTER
	MOV.B	#ZCLMFM,R2		; SET COMMAND FORMAT FLAG
	CLR.W	R1			; CLEAR CHARACTER PITCH (= 0)
	BSR	STCOMN:16		; SET COMMON MODE TO TEXT BUFFER
	MOV.W	@SP+,R0 		;+ UNSAVE FUNCTION CODE (DIRECTION FLAG)
	BSR	SVCC_3			; GET SPEED/PITCH FLAG
	CMP.B	#ZTBCLU,R0		;
	BEQ	SVCC_7			; BR. IF COLUMN COMMAND (UNDERLINE)
	SWAP	R1			;
	MOV.B	R0,R1			; SET DIRECTION FLAG
	MOV.W	R1,@(YSPDPF,FP) 	; SET DIRECTION FLAG & SPEED/PITCH FLAG
	MOV.W	@SP+,R0 		;+ UNSAVE RELATIVE COLUMN
	CLR.W	@(YRSERV,FP)		; CLEAR RESERVE AREA
	MOV.W	R0,@(YRCLMC,FP) 	; SAVE RELATIVE COLUMN TO TEXT BUFFER
	RTS
	;
SVCC_3: JSR	@GPTPM			; GET PICTH PARAMETER (= R2)
	SHLL.B	R2			; SHIFT 4 TIMES (MAKE HIGH NIBBLE)
	SHLL.B	R2			;
	SHLL.B	R2			;
	SHLL.B	R2			;
	MOV.B	#Z10ASP,R1		; SET KANJI SPEED FLAG
;
;-0609	BTST.B	#QKJMD,@PTMD5F:8	;
;-0609	BNE	SVCC_4			; BR. IF KANJI MODE
	BTST.B	#QKCSWA,@UMOD01:8	; +0609
	BEQ	SVCC_4			; +0609
	MOV.B	#Z30SPD,R1		; SET DRAFT SPEED FLAG
	BTST.B	#QPROPO,@PTMD1F:8	;
	BNE	SVCC_2			; BR. IF PROPORTIONAL MODE
	BTST.B	#QLQMD,@PTMD2F:8	;
	BEQ	SVCC_G			; BR. IF NOT LQ MODE
SVCC_2: MOV.B	#Z10BSP,R1		; SET LQ SPEED FLAG
SVCC_4: BTST.B	#QKNJHS,@PSPDFS:8	;
	BEQ	SVCC_6			; BR. IF NOT KANJI HIGH SPEED MODE
	MOV.B	@PSPDFS:8,R3		; GET PANEL SPEED
	BCLR.B	#QKNJHS,R3		; RESET KANJI HIGH SPEED FLAG
	CMP.B	#ZSPD15,R3		;
	BCC	SVCC_F			; BR. IF PANEL SPEED >= SUPER SPEED
	MOV.B	#Z20SPD,R3		; SET KANJI HIGH SPEED FLAG
	BRA	SVCC_F
	.PAGE
SVCC_6: MOV.B	@PSPDFS:8,R3		; GET PANEL SPEED SPEED FLAG
	BCLR.B	#QKNJHS,R3		; RESET KANJI HIGH SPEED FLAG
	CMP.B	#ZSPD10,R3		;
	BNE	SVCC_C			; BR. IF NOT PANEL = LOW SPEED
	MOV.B	#Z10SPD,R3		; SET ALTER SPEED FLAG
	BTST.B	#QSPDB,@APL2F:8 	;
	BNE	SVCC_F			; BR. IF EDITING MODE
;-0609	BTST.B	#QKJMD,@PTMD5F:8	;
;-0609	BNE	SVCC_A			; BR. IF KANJI MODE
	BTST.B	#QKCSWA,@UMOD01:8	; +0609
	BEQ	SVCC_A			; +0609
	BTST.B	#QDLSEL,@PTMD0F:8	;
	BEQ	SVCC_G			; BR. IF NOT SELECT DOWNLOAD
	TST.B	R0			;
	BPL	SVCC_G			; BR. IF STANDARD / GRAPHIC CODE
	BTST.W	#QAKCDH,R0		;
	BEQ	SVCC_G			; BR. IF KATAKANA CODE
	BRA	SVCC_F			; BR. DOWNLOAD SPEED FLAG
	;
SVCC_C: ADDS.B	#4,R3			; ADJUST PANEL SPEED
	BRA	SVCC_F
	;
SVCC_A: CMP.B	#ZGJCD,@KJCDS1:8	;
	BNE	SVCC_G			; BR. IF NOT GAIJI CODE
SVCC_F: MOV.B	R3,R1			; EXCHANGE SPEED FLAG
SVCC_G: OR.B	R2,R1			; SET SPEED / PITCH FLAG
	RTS
;
;	<< RELATIVE FORWARD UNDERLINE >>
;
SVCC_7: MOV.B	#ZANKFM,@(YFMATF,FP)	; REWRITE FORMAT FLAG
	JSR	@GPTPM			; GET PITCH PARAMETER (= R2)
	BSET.B	R2,@PRPTCF:8		; SET PRINT PITCH FLAG (1 LINE)
	SWAP	R1			;
	MOV.B	R0,R1			; SET PRE. SPACE
	MOV.W	R1,@(YSPDPF,FP) 	; SET PRE. SPACE & SPEED/PITCH FLAG
	MOV.W	@SP+,R0 		;+ UNSAVE RELATIVE COLUMN
	MOV.W	#ZSP,@(YRCLMC,FP)	; SET DUMMY CODE (H'20)
	MOV.W	#XRELUL,@(YRSERV,FP)	; SET MODE FLAG (UNDERLINE)
	BTST.B	#QVENLR,@PTMD0F:8	;
	BEQ	SVCC_8			; BR. IF NOT VERTICAL ENLARGE FOR ANK
	BSET.W	#QAVENL,@(YRSERV,FP)	; SET MODE FLAG (VERTICAL ENLARGE)
SVCC_8: RTS
	.PAGE
;
; << SUB >>	SAVE FEED COMMAND FORMAT TO TEXT BUFFER
;
;	IN	DATLNS : FEED PARAMETER
;		    R5 : FUNCTION CODE
;
SVFCM:	MOV.W	@DATLNS:8,R0		; GET FEED PARAMETER
	MOV.W	R0,@-SP 		;+ SAVE FEED PARAMETER
	MOV.W	#ZPFORJ,R1		; SET DP3 PACKET CODE (ESC + J)
	CMP.B	#ZTBFDF,R5		;
	BEQ	SVFC_2			; BR. IF FORTH TO FUNCTION CODE
	MOV.W	#ZPREVJ,R1		; SET DP3 PACKET CODE (ESC + j)
SVFC_2: MOV.W	R1,@-SP 		;+ SAVE PACKET CODE
	BSR	CKTXTB			; CHECK TEXT BUFFER POINTER
	MOV.B	#ZFEDFM,R2		; SET COMMAND FORMAT FLAG
	CLR.W	R1			; CLEAR CHARACTER PITCH (= 0)
	BSR	STCOMN			; SET COMMON MODE TO TEXT BUFFER
	MOV.W	@SP+,R0 		;+ UNSAVE PACKET CODE
	CLR.B	@(YSPDPF,FP)		; CLEAR SPEED/PITCH PARAMETER
	CLR.W	@(YMODEF,FP)		; CLEAR RESERVE AREA
	MOV.B	R0,@(YPKTCD,FP) 	; SAVE DP3 PACKET FALG
	MOV.W	@SP+,R0 		;+ UNSAVE FEED PARAMETER
	MOV.W	R0,@(YABSFD,FP) 	; SAVE FEED PARAMETER TO TEXT BUFFER
	RTS
	.PAGE
;
; << SUB >>	CHECK TEXT BUFFER OVER
;
;	IN	NONE
;
;	OUT	FP : TEXT BUFFER POINTER
;
;	USE	R1->FP
;
CKTXTB: CMP.W	#TXTBF,@TXTBFP:8	;
	BNE	CKTX_8			; BR. IF NOT DATA START
	MOV.W	@STCLMC:8,R1		; GET START COLUMN COUNTER
	CMP.W	@LMARGS:8,R1		;
	BNE	CKTX_2			; BR. IF NOT LINE START
	MOV.B	@PSPDF:8,R1		; GET PRINT SPEED (PANEL)
	MOV.B	R1,@PSPDFS:8		; SET PRINT SPEED (LINE)
	BTST.B	#QKHSMD,@PTMD5F:8	;
	BEQ	CKTX_4			; BR. IF NOT KANJI HIGH SPEED MODE
	BSET.B	#QKNJHS,@PSPDFS:8	; SET KANJI HIGH SPEED MODE
CKTX_4: BSR	SETPEL			; SET PANEL ENLARGE
CKTX_8: MOV.W	@TXTBFP:8,R3		; GET TEXT BUFFER POINTER
	CMP.W	#TXTBE,R3		;
	BCS	CKTX_6			; BR. IF NOT TEXT BUFFER OVER
	BSR	STPRNT			; SET END MARK TO TEXT BUFFER & PRINT
	JSR	@INBFC			; SET NEXT LINE START = CURRENT COLUMN
CKTX_6: MOV.W	@TXTBFP:8,FP		; SET TEXT BUFFER POINTER
	RTS
	;
CKTX_2: BSR	SPEL_4			; CHECK COPY MODE FLAG
	BRA	CKTX_8
	;
SETPEL: JSR	@GETPFP 		; GET CURRENT FORMAT No.
	MOV.B	@(E2CHSZ,FP),R1 	; GET CURRENT FORMAT ENLARGE SIZE
	MOV.B	R1,@PNLELS:8		; SAVE IT
SPEL_4: BCLR.B	#QCP7P,@COLORF:8	; RESET PANEL 7P MODE (1 LINE)
	BTST.B	#Q7PM,@PRMDF:8		;
	BEQ	SPEL_2			; BR. IF PANEL 5P MODE
	BSET.B	#QCP7P,@COLORF:8	; SET PANEL 7P MODE (1 LINE)
SPEL_2: RTS
;
; << SUB >>	SET COMMON MODE TO TEXT BUFFER
;
;	IN	R1 : CHARACTER PITCH (PRINT DATA NOT = 0)
;		R2 : COMMAND FORMAT FLAG
;		FP : TEXT BUFFER POINTER
;
;	OUT	FP : TEXT BUFFER POINTER
;			@(YFMATF,FP) -> EACH FORMAT FLAG	(1 byte)
;			@(YCOLRF,FP) -> CURRENT COLOR FLAG	(1 byte)
;			@(YCLMCF,FP) -> CURRENT COLUMN COUNTER	(2 byte)
;
;	USE	R0->FP
;
STCOMN: MOV.B	R2,@(YFMATF,FP) 	; SAVE FORMAT FLAG
STCM_0:
	MOV.B	@PTMD3F:8,R3		; GET PRINT MODE 3
	AND.B	#XCOLMK,R3		; MASK EXCEPT COLOR BIT FLAG
STCM_1: BTST.B	#QDBSTR,@PTMD1F:8	;
	BEQ	STCM_2			; BR. IF NOT DOUBLE-STRIKE MODE
	BTST.B	#QCP7P,@COLORF:8	;
	BNE	STCM_2			; BR. IF COPY 7P MODE (1 LINE)
	BSET.B	#7,R3			; SET DOUBLE-STRIKE FLAG
STCM_2: MOV.B	R3,@(YCOLRF,FP) 	; SAVE COLOR FLAG
	MOV.W	@CLMCTC:8,R3		; GET COLUMN COUNTER
	MOV.W	R3,@(YCLMCF,FP) 	; SAVE COLUMN COUNTER
	CMP.B	#ZCMBFM,R2		;
	BHI	STCM_4			; BR. IF COLUMN/FEED/IMAGE/BAR-CODE FLAG
	ADD.W	R1,R3			; + CHARACTER PITCH
	MOV.W	R3,@CLMCTC:8		; UPDATE COLUMN COUNTER
STCM_4: RTS
	.END

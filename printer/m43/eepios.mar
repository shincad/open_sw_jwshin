	.PROGRAM	eepios
	.HEADING	"M4324 EEPROM I/O SYSTEM MODULE"
	.LIST		NOCOND,NOEXP
;********************************************************
;							*
;	M4324 EEPROM I/O SYSTEM MODULE			*
;							*
;	FILE NAME	EEPIOS.H			*
;	CREATED		14/NOV/1990			*
;							*
;********************************************************
;
	.EXPORT		EEWRDI,EEWREN
	.EXPORT		EERDWD,EERD,  EEWRWD,EERDBF,EEWRBF

	.IMPORT		IOSTCB

	.INCLUDE	"COMMON.H"
	.INCLUDE	"M43RTM.H"
	.INCLUDE	"CPUIO.H"

;
;	EEPROM(M6M80021) INSTRUCTION CODE
;
ZEEWD:	.EQU	H'0500			; WRITE DISABLE
ZEEWE:	.EQU	H'C500			; WRITE ENABLE
ZEERD:	.EQU	H'1500			; READ
ZEEWR:	.EQU	H'2500			; WRITE
ZEESB:	.EQU	H'9500			; BUSY STATUS OUTPUT
	.PAGE
	.SECTION	SYS1,CODE,ALIGN=16
;
;	BUFFER READ FROM EEPROM
;
;	IN	R3 : EEPROM ADDRESS
;	     EP R4 : BUFFER POINTER
;		R5 : WORD SIZE
;
;	OUT	NONE
;
EERDBF:	ADD.W	#-1,R5			; ADJUST FOR LOOP
EERD_1:	BSR	EERD			; READ WORD DATA FROM EEPROM
	ADD.B	#1,R3			; UPDATE EEPROM ADDRESS
	MOV.W	R2,@R4+			; SAVE READ DATA INTO BUFFER
	SCB/F	R5,EERD_1		;
	RTS				;

;
;	WORD READ
;
;	IN	R3 : EEPROM ADDRESS
;
;	OUT	R2 : READ WORD DATA
;		R3 : NOT CHANGE
;
EERDWD:	BSR	EERD			; READ DATA FROM EEPROM
	RETR2	@IOSTCB			; RETURN READ DATA
	RTS				;
;
EERD:	BCLR.B	#QEECS,@P5DR		; SELECT EEPROM
	MOV.W	#ZEERD,R0		; SET READ INSTRUCTION
	OR.B	R3,R0			; SET EEPROM ADDRESS
	BSR	EEOP			; OUTPUT INSTRUCTION CODE
	BSR	EERD_2			; READ 8 BIT
	;				; DELAY FOR SCK HIGH LEVEL
	BSR	EERD_2			; READ 8 BIT
	SWAP	R2			;
	BSET.B	#QEECS,@P5DR		; DESELECT EEPROM
	RTS				;
	;
EERD_2:	MOV.W	#ZBYTE-1,R1		; SET LOOP COUNTER
EERD_3:	BCLR.B	#QEECK,@P5DR		; SET SCK=LOW
	MOV.B	@P5DR,R0		;
	BSET.B	#QEECK,@P5DR		; SET SCK=HIGH
	SHLL.B	R0			;
	SHLL.B	R0			; CHECK DO
	ROTXR.W	R2			; COPY DO BIT STATUS
	SCB/F	R1,EERD_3		;
	RTS				;
	.PAGE
;
;	WRITE DISABLE/ENABLE
;
;	IN	NONE
;
;	OUT	NONE
;
EEWRDI:	MOV.W	#ZEEWD,R0		; SET WRITE DISABLE INSTRUCTION
	BRA	EEWR_1			;

EEWREN:	MOV.W	#ZEEWE,R0		; SET WRITE ENABLE INSTRUCTION
EEWR_1:	BCLR.B	#QEECS,@P5DR		; SELECT EEPROM
	BSR	EEOP			; OUTPUT INSTRUCTION CODE
	BSET.B	#QEECS,@P5DR		; DESELECT EEPROM
	RTS				;

;
;	OUTPUT DATA TO EEPROM
;
;	IN	R0 : OUTPUT DATA
;
;	OUT	NONE
;
EEOP:	SWAP	R0			;
	BSR	EEOP_1			; OUTPUT 8 BIT
	STC.W	SR,R1			; DELAY FOR SCK HIGH LEVEL
	BSR	EEOP_1			; OUTPUT 8 BIT
	RTS				;
	;
EEOP_1:	MOV.W	#ZBYTE-1,R1		; SET LOOP COUNTER
EEOP_2:	SHLR.W	R0			;
	BCC	EEOP_3			; BR IF BIT OFF
	BCLR.B	#QEECK,@P5DR		; SET SCK=LOW
	BSET.B	#QEEDI,@P5DR		; SET DI =HIGH
	BSET.B	#QEECK,@P5DR		; SET SCK=HIGH
	SCB/F	R1,EEOP_2		;
	RTS				;
					;
EEOP_3:	BCLR.B	#QEECK,@P5DR		; SET SCK=LOW
	BCLR.B	#QEEDI,@P5DR		; SET DI =LOW
	BSET.B	#QEECK,@P5DR		; SET SCK=HIGH
	SCB/F	R1,EEOP_2		;
	RTS				;
	.PAGE
;
;	WORD WRITE
;
;	IN	R2 : WRITE WORD DATA
;		R3 : EEPROM ADDRESS
;
;	OUT	R3 : NOT CHNAGE
;
EEWRWD:	MOV.W	#ZEEWR,R0		; SET WRITE INSTRUCTION
	OR.B	R3,R0			; SET EEPROM ADDRESS
	BCLR.B	#QEECS,@P5DR		; SELECT EEPROM
	BSR	EEOP			; OUTPUT INSTRUCTION CODE
	MOV.W	R2,R0			; GET WRITE DATA
	BSR	EEOP			; OUPUT DATA
	MOV.W	#ZBYTE-1,R1		;
EEWR_2:	BSR	EEWR_4			; DELAY FOR STATUS OUTPUT
	SCB/F	R1,EEWR_2		;
	MOV.W	#ZEESB,R0		; SET BUSY STATUS INSTRIUCTION
	BSR	EEOP			; OUTPUT INSTRUCTION CODE
EEWR_3:	BCLR.B	#QEECK,@P5DR		; SET SCK=LOW
	MOV.B	@P5DR,R0		;
	BSET.B	#QEECK,@P5DR		; SET SCK=HIGH
	BTST.B	#QEEDO,R0		;
	BEQ	EEWR_3			; BR IF WRITE BUSY
	BSET.B	#QEECS,@P5DR		; DESELECT EEPROM
EEWR_4:	RTS				;

;
;	BUFFER WRITE TO EEPROM
;
;	IN	R3 : EEPROM ADDRESS
;	     EP R4 : BUFFER POINTER
;		R5 : WORD SIZE
;
;	OUT	NONE
;
EEWRBF:	ADD.W	#-1,R5			; ADJUST FOR LOOP
EEWR_5:	MOV.W	@R4+,R2			; GET WORD DATA
	BSR	EEWRWD			; WRITE DATA TO EEPROM
	ADD.B	#1,R3			; UPDATE EEPROM ADDRESS
	SCB/F	R5,EEWR_5		;
	RTS				;
	.END
